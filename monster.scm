; ---- Monster ----
(define (monster-maker health take-damage power)
  (if (<= health 0)
    '()
    (lambda (action . attack-instance)
      (case action
        ((get-attacked) (monster-maker (- health (take-damage (car attack-instance)))
                                        take-damage
                                        power))
        ((attack) power)))))

(define (new-monster-maker type)
  (let ((health (health-maker type))
        (take-damage (damage-taker-maker type))
        (damage (monster-attack-maker type)))
    (monster-maker health take-damage damage)))

(define (health-maker type)
  (case type
    ((zombie) (integer-between 50 100))
    ((vampire) (integer-between 100 200))
    ((ghoul) (integer-between 40 80))
    ((werewolf) 200)
    ((human) 5)))

(define (monster-attack-maker type)
  (case type
    ((zombie) (integer-between 0 10))
    ((vampire) (integer-between 10 20))
    ((ghoul) (integer-between 15 30))
    ((werewolf) (integer-between 0 40))
    ((human) 5)))

(define (damage-taker-maker type)
  (case type
    ((zombie)
     (lambda (attack)
       (case (attack 'type)
         ((straw) (* 2 (attack 'damage)))
         (else (attack 'damage)))))
    ((vampire)
     (lambda (attack)
       (case (attack 'type)
         ((chocolate) 0)
         (else (attack 'damage)))))
    ((ghoul)
     (lambda (attack)
       (case (attack 'type)
         ((nerd) (* 5 (attack 'damage)))
         (else (attack 'damage)))))
    ((werewolf)
     (lambda (attack)
       (case (attack 'type)
         ((chocolate) 0)
         ((straw) 0)
         (else (attack 'damage)))))
    ((human) (lambda (attack) 0))))
